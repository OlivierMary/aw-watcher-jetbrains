plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.13.1'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group 'fr.mary.olivier'
version '2.0.6'

repositories {
    mavenCentral()
}

sourceSets {
    generated {
        java.srcDirs "${projectDir}/src/generated/java", "${projectDir}/swagger/src/main/java"
    }
    main {
        java.srcDirs += sourceSets.generated.java.srcDirs
    }
}

tasks.register('generateVersion') {
    doFirst {
        def versionFile = file("${projectDir}/src/generated/java/fr/mary/olivier/aw/watcher/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package fr.mary.olivier.aw.watcher;

public class Version {
	public static String getVersion() {
		return "$project.version";
	}
}
"""
    }
}

swaggerSources {
    activityWatcher {
        inputFile = file('src/main/resources/swagger.yaml')

        code {
            language = 'java'
            outputDir = file('swagger')
            wipeOutputDir = true
            rawOptions = ["--ignore-file-override=${projectDir}/.openapi-generator-ignore"]

        }
    }
}

compileJava.dependsOn generateVersion, swaggerSources.activityWatcher.code

clean {
    delete += sourceSets.generated.java.srcDirs + sourceSets.generated.resources.srcDirs + swaggerSources.activityWatcher.code.outputDir
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


dependencies {
    implementation 'io.swagger:swagger-annotations:1.6.9'
    implementation 'com.squareup.okhttp:okhttp:1.6.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.gsonfire:gson-fire:1.8.3'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.openapitools:openapi-generator:6.4.0'
    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testImplementation 'junit:junit:4.13.1'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:6.4.0'
}

intellij {
    version.set('2020.3')
    setUpdateSinceUntilBuild(false)
    plugins.add('Git4Idea')

}
patchPluginXml {
    setChangeNotes """
      2.0.6<br>
       - Upgrade dependencies<br>
      2.0.5<br>
       - Fix initial warning<br>
      2.0.4<br>
       - Fix aw-server-rust schema differences<br>
       - Fix very slow aw-server issues (windows)<br>
       - Use async http client<br>
      2.0.3<br>
       - Fix unknown editor name in final version<br>
      2.0.2<br>
       - Add more compatibility<br>
      2.0.1<br>
       - Fix Call to Internal API<br>
      2.0.0<br>
       - Add support for IntelliJ 2022.3<br>
       - Change to HeatBeat event<br>
       - Add VCS informations<br>
      1.0.8<br>
       - Fix exceptions<br>
       - Fix file name recorded<br>
       - Fix multiple events recorded<br>
      1.0.7<br>
       - Fix notes + plugin.xml comments<br>
      1.0.6<br>
       - Compatibility<br>
      1.0.5<br>
       - Send notification when connexion is back<br>
      1.0.4<br>
       - All events resend if connexion to AW server lost<br>
       - No more error log if connexion failed after ide start connexion to AW server<br>
      1.0.3<br>
       - Change plugin name<br>
      1.0.2<br>
       - Fix NPE.<br>
      1.0.1<br>
       - Fix build.<br>
      1.0.0<br>
       - Initial version"""
}

publishPlugin {
    setToken intellijPublishToken
}