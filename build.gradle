plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.3.12'
}

group 'fr.mary.olivier'
version '1.0.7'

sourceSets {
    generated {
        java.srcDir "${projectDir}/src/generated/java"
    }
    main {
        java.srcDirs += sourceSets.generated.java.srcDirs
    }
}

task generateVersion {
    doFirst {
        def versionFile = file("${projectDir}/src/generated/java/fr/mary/olivier/aw/watcher/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package fr.mary.olivier.aw.watcher;

public class Version {
	public static String getVersion() {
		return "$project.version";
	}
}
"""
    }
}
compileJava.dependsOn generateVersion

clean {
    delete += sourceSets.generated.java.srcDirs + sourceSets.generated.resources.srcDirs
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.swagger:swagger-annotations:1.5.15'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'io.gsonfire:gson-fire:1.8.0'
    compile 'org.threeten:threetenbp:1.4.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

intellij {
    version 2017.3 as String // since this version.
    updateSinceUntilBuild false
}
patchPluginXml {
    changeNotes """
      1.0.7<br>
       - Fix notes + plugin.xml comments<br>
      1.0.6<br>
       - Compatibility<br>
      1.0.5<br>
       - Send notification when connexion is back<br>
      1.0.4<br>
       - All events resend if connexion to AW server lost<br>
       - No more error log if connexion failed after ide start connexion to AW server<br>
      1.0.3<br>
       - Change plugin name<br>
      1.0.2<br>
       - Fix NPE.<br>
      1.0.1<br>
       - Fix build.<br>
      1.0.0<br>
       - Initial version"""
}

publishPlugin {
    username intellijPublishUsername
    password intellijPublishPassword
}