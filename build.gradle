plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.10'
    id 'org.hidetake.swagger.generator' version '2.18.1'
}

group 'fr.mary.olivier'
version '1.0.8'

sourceSets {
    generated {
        java.srcDirs "${projectDir}/src/generated/java", "${projectDir}/swagger/src/main/java"
    }
    main {
        java.srcDirs += sourceSets.generated.java.srcDirs
    }
}

task generateVersion {
    doFirst {
        def versionFile = file("${projectDir}/src/generated/java/fr/mary/olivier/aw/watcher/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package fr.mary.olivier.aw.watcher;

public class Version {
	public static String getVersion() {
		return "$project.version";
	}
}
"""
    }
}

swaggerSources {
    activityWatcher {
        inputFile = file('src/main/resources/swagger.yaml')

        code {
            language = 'java'
            outputDir = file('swagger')
            wipeOutputDir = true
            rawOptions = ["--ignore-file-override=${projectDir}/.openapi-generator-ignore"]

        }
    }
}

compileJava.dependsOn generateVersion, swaggerSources.activityWatcher.code

clean {
    delete += sourceSets.generated.java.srcDirs + sourceSets.generated.resources.srcDirs + swaggerSources.activityWatcher.code.outputDir
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.swagger:swagger-annotations:1.5.22'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'io.gsonfire:gson-fire:1.8.3'
    compile 'org.threeten:threetenbp:1.5.1'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'org.openapitools:openapi-generator:4.1.0'
    compile 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.common:1.0.1'
    compile 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'
}

intellij {
    version 2017.3 as String // since this version.
    updateSinceUntilBuild false
}
patchPluginXml {
    changeNotes """
      1.0.8<br>
       - Fix exceptions<br>
       - Fix file name recorded<br>
       - Fix multiple events recorded<br>
      1.0.7<br>
       - Fix notes + plugin.xml comments<br>
      1.0.6<br>
       - Compatibility<br>
      1.0.5<br>
       - Send notification when connexion is back<br>
      1.0.4<br>
       - All events resend if connexion to AW server lost<br>
       - No more error log if connexion failed after ide start connexion to AW server<br>
      1.0.3<br>
       - Change plugin name<br>
      1.0.2<br>
       - Fix NPE.<br>
      1.0.1<br>
       - Fix build.<br>
      1.0.0<br>
       - Initial version"""
}

publishPlugin {
    token intellijPublishToken
}